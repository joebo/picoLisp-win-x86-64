<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN" "http://www.w3.org/TR/1998/REC-html40-19980424/loose.dtd">
<html lang="en">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<title>Q</title>
<link rel="stylesheet" href="../doc.css" type="text/css">
</head>
<body>

<h1>Q</h1>

<dl>
    <dt><a name="+QueryChart"><code>+QueryChart</code></a>
    <dd>In addition to the 'standard' arguments passes to charts, the
    +QueryChart takes two additional ones. First, the number of initial rows to
    fetch and second a Pilog query that retrieves data. Typically used in search
    dialogs, as the following example from the demo app supplied with the
    PicoLisp distribution.

    <pre></code>
    (de choItem (Dst)
       (diaform '(Dst)
          (<grid> "--.-."
             ,"Number" (gui 'nr '(+Focus +Var +NumField) '*ItemNr 10)
             ,"Supplier" (gui 'sup '(+Var +TextField) '*ItemSup 20)
             (searchButton '(init&gt; (: home query)))
             ,"Description" (gui 'nm '(+Var +TextField) '*ItemNm 30)
             ,"Price" (gui 'pr '(+Var +FixField) '*ItemPr 2 12)
             (resetButton '(nr nm pr sup query)) )
          (gui 'query '(+QueryChart) (cho)
             '(goal
                (quote
                   @Nr (and *ItemNr (cons @ T))
                   @Nm *ItemNm
                   @Pr (and *ItemPr (cons @ T))
                   @Sup *ItemSup
                   (select (@@)
                      ((nr +Item @Nr) (nm +Item @Nm) (pr +Item @Pr) (nm +CuSu @Sup (sup +Item)))
                         (range @Nr @@ nr)
                         (part @Nm @@ nm)
                         (range @Pr @@ pr)
                         (tolr @Sup @@ sup nm) ) ) )
             6
             '((This) (list This (: nr) This (: sup) (: sup ort) (: pr))) )
             (&lt;table&gt; 'chart (choTtl ,"Items" 'nr '+Item)
                (quote
                   (btn)
                   (align "#")
                   (NIL ,"Description")
                   (NIL ,"Supplier")
                   (NIL ,"City")
                   (align ,"Price") )
                (do (cho)
                   (<row> (alternating)
                      (gui 1 '(+DstButton) Dst)
                      (gui 2 '(+NumField))
                      (gui 3 '(+ObjView +TextField) '(: nm))
                      (gui 4 '(+ObjView +TextField) '(: nm))
                      (gui 5 '(+TextField))
                      (gui 6 '(+FixField) 2) ) ) )
             (<spread>
                (scroll (cho))
                (newButton T Dst '(+Item)
                '(nr genKey 'nr '+Item)
                'nm *ItemNm
                'pr *ItemPr )
                (cancelButton) ) ) )

    </code></pre>

    The first argument to +QueryChart, <code>(cho)</code>, returns a number that
    decides how many rows of data should be fetched. The same call is used a
    little later to draw the table presenting the data as well. Following that
    argument is the Pilog query used to fetch the data we wish to display.

    <pre><code>
    '(goal
       (quote
          @Nr (and *ItemNr (cons @ T))
          @Nm *ItemNm
          @Pr (and *ItemPr (cons @ T))
          @Sup *ItemSup
          (select (@@)
             ((nr +Item @Nr) (nm +Item @Nm) (pr +Item @Pr) (nm +CuSu @Sup (sup +Item)))
                (range @Nr @@ nr)
                (part @Nm @@ nm)
                (range @Pr @@ pr)
                (tolr @Sup @@ sup nm) ) ) )
    </code></pre>

    The Pilog query is followed by a number, in this case 6, that tells how many
    columns are used when presenting the data. This is the normal column count,
    as inherited from +Chart, and it matches up with the number of columns in
    our table.

    <pre><code>
    (&lt;table&gt; 'chart (choTtl ,"Items" 'nr '+Item)
       (quote
          (btn)
          (align "#")
          (NIL ,"Description")
          (NIL ,"Supplier")
          (NIL ,"City")
          (align ,"Price") )
       (do (cho)
          (&lt;row&gt; (alternating)
             (gui 1 '(+DstButton) Dst)
             (gui 2 '(+NumField))
             (gui 3 '(+ObjView +TextField) '(: nm))
             (gui 4 '(+ObjView +TextField) '(: nm))
             (gui 5 '(+TextField))
             (gui 6 '(+FixField) 2) ) ) )
    </code></pre>

    The +QueryChart in the example uses a 'put'- function to put data into the
    GUI components from the result of the Pilog query.

    <pre><code>
    '((This) (list This (: nr) This (: sup) (: sup ort) (: pr)))
    </code>

    The 'put'- function is applied to each row and places the desired content
    into the GUI. Since this chart is readonly, i.e. contains no input fields,
    the corresponding 'get'- function can be omitted. Each item in the list
    produced by our 'put'- function matches up with a column in the table
    presenting the result. The first item, <code>This</code>, is used to create
    a link to the object, in column 1 of the table: <code>(gui 1 '(+DstButton)
    Dst)</code>

    Item number 2, <code>(: nr)</code>, maps to the second column, showing the
    number of the object in question: <code>(gui 2 '(+NumField))</code>

    The two following items, <code>This</code> and <code>(: sup)</code> each map
    to a combination of +ObjView and +TextField:

    <pre><code>
    (gui 3 '(+ObjView +TextField) '(: nm))
    (gui 4 '(+ObjView +TextField) '(: nm))
    </code></pre>

    The +ObjView is used to create a link to an object, in both cases displaying
    the <code>(: nm)</code> property of each object.

    Finally, the name of the town where the supplier is located and the price of
    the item are displayed. The 'put'- function supplies these values: <code>(:
    sup ort)</code> and <code>(: pr)</code>, which are mapped to the following
    columns in the table:

    <pre><code>
    (gui 5 '(+TextField))
    (gui 6 '(+FixField) 2)
    </code></pre>

    See also <a href="refC.html#+Chart">+Chart</a>, <a
    href="refC.html#cho">cho</a>, <a href="refG.html#gui">gui</a>, <a
    href="refD.html#+DstButton">+DstButton</a>, <a
    href="refN.html#+NumField">+NumField</a>, <a
    href="refD.html#diaform">diaform</a>, <a
    href="refO.html#+ObjView">+ObjView</a>, <a
    href="refT.html#+TextField">+TextField</a>, <a
    href="refF.html#+FixField">+FixField</a>,<a
    href="../app.html#charts">charts</a> and <a href="../select.html">the select
    predicate</a>
</dl>

</body>
</html>
